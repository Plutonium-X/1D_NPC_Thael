/////////////////////////////////////////////////////////////////////
// Preparativos
/////////////////////////////////////////////////////////////////////
IF
	
	Global("Prep","LOCALS",0)
        OR(2)
            Allegiance(Myself,ENEMY)
            See([GOODCUTOFF])

THEN
	RESPONSE #100

		SetGlobalTimer("Cast","LOCALS",1)
		SetGlobalTimer("TimeStop","LOCALS",1)

                DisplayString(Myself,39968) //Disparador de conjuros - activado
                ReallyForceSpell(Myself,WIZARD_MELF_METEOR)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
		ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)

		SetGlobalTimer("RecastProMagWeap","LOCALS",24)

		ReallyForceSpell(Myself,WIZARD_SPIRIT_ARMOR) 
		ReallyForceSpell(Myself,WIZARD_SPELL_TRAP) 
		ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)

		SetGlobal("Prep","LOCALS",1)

                GiveItemCreate("potn34",Myself,1,0,0)
                UseItem("potn34",Myself)

		// Rememorizar

                SetGlobal("ChainContingency","LOCALS",0)
                SetGlobal("ContBelial","LOCALS",0)
                SetGlobal("ContBelzebu","LOCALS",0)
                SetGlobal("RuLevistus","LOCALS",0)
                SetGlobal("Apoca","LOCALS",0)

                SetGlobal("Localizar","LOCALS",2)

		SetGlobal("Time","LOCALS",3)
                SetGlobal("Cometa","LOCALS",3)

		SetGlobal("Wilt","LOCALS",4)
	        SetGlobal("PowerWord","LOCALS",2)

		SetGlobal("Mordy","LOCALS",4)
		SetGlobal("Turn","LOCALS",2)

                SetGlobal("ProMagWeap","LOCALS",3)
                SetGlobal("DeathSp","LOCALS",3)

                SetGlobal("Chaos","LOCALS",6)
                

                SetGlobal("Stoneskin","LOCALS",4)
                SetGlobal("Malison","LOCALS",2)
                
                SetGlobal("MeteMelf","LOCALS",3)
                SetGlobal("Remove","LOCALS",3)
                SetGlobal("Slow","LOCALS",2)

		SetGlobal("RayoDebil","LOCALS",2)
		SetGlobal("Melf","LOCALS",4)

		SetGlobal("Missile","LOCALS",6)

		Continue()
END


//****************************************************


/////////////////////////////////////////////////////////////////////
// Contingencia en Cadena
/////////////////////////////////////////////////////////////////////
IF
    See(NearestEnemyOf(Myself))
    TookDamage()
    Global("ChainContingency","LOCALS",0)
THEN
  RESPONSE #100
    DisplayString(Myself,26328) // Contingencia en cadena
    SetGlobal("ChainContingency","LOCALS",1)
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_DISPEL_MAGIC)
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END


/////////////////////////////////////////////////////////////////////
// Runa de Contingencia de Belial
/////////////////////////////////////////////////////////////////////
IF
    See(NearestEnemyOf(Myself))
    HPPercentLT(Myself,75)
    Global("ContBelial","LOCALS",0)
THEN
  RESPONSE #100
    DisplayStringHead(Myself,@0 /* ~Runa leida: Contingencia de Belial~ */)
    SetGlobal("ContBelial","LOCALS",1)
    ReallyForceSpell(Myself,WIZARD_IMPROVED_INVISIBILITY)
    ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_DIVINATION)
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END


/////////////////////////////////////////////////////////////////////
// Señal Oscura de Belzebuth
/////////////////////////////////////////////////////////////////////

IF
    See(NearestEnemyOf(Myself))
    Global("ContBelzebu","LOCALS",0)
	OR(3)
                See([ENEMY.0.0.MAGE_ALL])
                See([ENEMY.0.0.BARD_ALL])
                See([ENEMY.0.0.CLERIC_ALL])
        OR(8)
            StateCheck(LastSeenBy(Myself),STATE_BLESS)
            StateCheck(LastSeenBy(Myself),STATE_CHANT)
            StateCheck(LastSeenBy(Myself),STATE_DRAWUPONHOLYMIGHT)
            StateCheck(LastSeenBy(Myself),STATE_BLUR)
            StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
            !CheckStat(LastSeenBy(Myself),0,STONESKINS)
            HasBounceEffects(LastSeenBy(Myself))
            HasImmunityEffects(LastSeenBy(Myself))
           !HasItemEquiped("CLCK26",NearestEnemyOf(Myself)) // Capa Espejo
THEN
	RESPONSE #100

		DisplayStringHead(Myself,@1 /* ~Runa leida: Señal Oscura de Belzebuth~ */)
                ReallyForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
		ReallyForceSpell(LastSeenBy(Myself), WIZARD_SUMMON_HAKEASHAR)
                ReallyForceSpell(LastSeenBy(Myself), WIZARD_SUMMON_HAKEASHAR)
                ReallyForceSpell(LastSeenBy(Myself),WIZARD_WARDING_WHIP)
                SetGlobal("ContBelzebu","LOCALS",1)
END

/////////////////////////////////////////////////////////////////////
// APOCALIPSIS
/////////////////////////////////////////////////////////////////////


IF
        See(NearestEnemyOf(Myself))
        See(SecondNearestEnemyOf(Myself))
        Global("Apoca","LOCALS",0)
	HPPercentLT(Myself,50)
THEN
	RESPONSE #100

                SetGlobal("Apoca","LOCALS",1)
                DisplayString(Myself,@2 /* ~¡¡Aarrggg...Vosotros lo habéis querido!! - ¡¡ASMODEUS, SEÑOR DE LOS ABISMOS, AUXILIAME!!                                                                    .~ */)
                DisplayStringHead(Myself,@3 /* ~Runa Maestra Arcana activada: Lágrimas Primigenias de Asmodeus~ */)
                ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
                ReallyForceSpell(Myself,WIZARD_ABSOLUTE_IMMUNITY)
                ReallyForceSpell(Myself,WIZARD_METEOR_SWARM)
                ReallyForceSpell(SecondNearestEnemyOf(MySelf),WIZARD_METEOR_SWARM)
                SetGlobalTimer("lipsis","LOCALS",2)  
END             

//*******1

IF
                Global("Apoca","LOCALS",1)
                GlobalTimerExpired("lipsis","LOCALS")
                See(Player1) 
THEN
	RESPONSE #100          
		ReallyForceSpell(Player1,WIZARD_COMET)
                Wait(1)
                SetGlobal("Apoca","LOCALS",2)
END
IF
                Global("Apoca","LOCALS",1)
                GlobalTimerExpired("lipsis","LOCALS")
                !See(Player1)
THEN
	RESPONSE #100       
                SetGlobal("Apoca","LOCALS",2)
END

//********2

IF
                Global("Apoca","LOCALS",2)
                See(Player2)
THEN
	RESPONSE #100          
		ReallyForceSpell(Player2,WIZARD_COMET)
                Wait(1)
                SetGlobal("Apoca","LOCALS",3)
END
IF
                Global("Apoca","LOCALS",2)
                !See(Player2)
THEN
	RESPONSE #100       
                SetGlobal("Apoca","LOCALS",3)
END

//*********3

IF
                Global("Apoca","LOCALS",3)
                See(Player3)
THEN
	RESPONSE #100          
		ReallyForceSpell(Player3,WIZARD_COMET)
                Wait(1)
                SetGlobal("Apoca","LOCALS",4)
END
IF
                Global("Apoca","LOCALS",3)
                !See(Player3)
THEN
	RESPONSE #100     
                SetGlobal("Apoca","LOCALS",4)
END

//********4

IF
                Global("Apoca","LOCALS",4)
                See(Player4)
THEN
	RESPONSE #100          
		ReallyForceSpell(Player4,WIZARD_COMET)
                Wait(1)
                SetGlobal("Apoca","LOCALS",5)
END
IF
                Global("Apoca","LOCALS",4)
                !See(Player4)
THEN
	RESPONSE #100     
                SetGlobal("Apoca","LOCALS",5)
END

//********5

IF
                Global("Apoca","LOCALS",5)
                See(Player5)
THEN
	RESPONSE #100          
		ReallyForceSpell(Player5,WIZARD_COMET)
                Wait(1)
                SetGlobal("Apoca","LOCALS",6)
END
IF
                Global("Apoca","LOCALS",5)
                !See(Player5)
THEN
	RESPONSE #100     
                SetGlobal("Apoca","LOCALS",6)
END

//********6

IF
                Global("Apoca","LOCALS",6)
                See(Player6)
THEN
	RESPONSE #100          
		ReallyForceSpell(Player6,WIZARD_COMET)
                Wait(1)
                SetGlobal("Apoca","LOCALS",7)
END
IF
                Global("Apoca","LOCALS",6)
                !See(Player6)
THEN
	RESPONSE #100     
                SetGlobal("Apoca","LOCALS",7)
END


//**********************************************************************************
//IF
//                Global("Apoca","LOCALS",1)
//                GlobalTimerExpired("lipsis","LOCALS")    
//THEN
//	RESPONSE #100          
//		ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_COMET)
//               Wait(1)
//                ReallyForceSpell(SecondNearestEnemyOf(Myself),WIZARD_COMET)
//                ReallyForceSpell(ThirdNearestEnemyOf(Myself),WIZARD_COMET)
//                Wait(1)
//                ReallyForceSpell(FourthNearestEnemyOf(Myself),WIZARD_COMET)
//                ReallyForceSpell(FifthNearestEnemyOf(Myself),WIZARD_COMET)
//                Wait(1)
//                ReallyForceSpell(SixthNearestEnemyOf(Myself),WIZARD_COMET)
//                SetGlobal("Apoca","LOCALS",2)
//END
//***********************************************************************************

/////////////////////////////////////////////////////////////////////
// Contingencia
/////////////////////////////////////////////////////////////////////
IF
    See(NearestEnemyOf(Myself))
    TookDamage()
    Global("Contingency","LOCALS",0)
THEN
    RESPONSE #100
        DisplayString(Myself,40252) // Contingencia
        ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
        SetGlobalTimer("RecastProMagWeap","LOCALS",24)
        SetGlobal("Contingency","LOCALS",1)
        SetGlobalTimer("Cast","LOCALS",6)
END


/////////////////////////////////////////////////////////////////////
// Localizar al enemigo 
/////////////////////////////////////////////////////////////////////

IF
        GlobalGT("Localizar","LOCALS",0)
    OR(2)
	!Detect(NearestEnemyOf(Myself))
	!Detect(Player1)
THEN

       RESPONSE #100
                DisplayString(Myself,@4 /* ~¡No puedes esconderte de mi!~ */)
                ForceSpell(Myself,WIZARD_TRUE_SIGHT)
                Wait(2)
		ForceSpell(Player1,WIZARD_DIMENSION_DOOR)
                IncrementGlobal("Localizar","LOCALS",-1)
                SetGlobal("DispaDoor","LOCALS",1)
    		
	RESPONSE #100
                DisplayString(Myself,@4 /* ~¡No puedes esconderte de mi!~ */)
                ForceSpell(Myself,WIZARD_TRUE_SIGHT)
                Wait(2)
		ForceSpell(Player2,WIZARD_DIMENSION_DOOR)
                IncrementGlobal("Localizar","LOCALS",-1)
                SetGlobal("DispaDoor","LOCALS",1)

	RESPONSE #100
                DisplayString(Myself,@4 /* ~¡No puedes esconderte de mi!~ */)
                ForceSpell(Myself,WIZARD_TRUE_SIGHT)
                Wait(2)
		ForceSpell(Player3,WIZARD_DIMENSION_DOOR)
                IncrementGlobal("Localizar","LOCALS",-1)
                SetGlobal("DispaDoor","LOCALS",1)

	RESPONSE #100
                DisplayString(Myself,@4 /* ~¡No puedes esconderte de mi!~ */)
		ForceSpell(Myself,WIZARD_TRUE_SIGHT)
                Wait(2)
		ForceSpell(Player4,WIZARD_DIMENSION_DOOR)
                IncrementGlobal("Localizar","LOCALS",-1)
                SetGlobal("DispaDoor","LOCALS",1)

	RESPONSE #100
                DisplayString(Myself,@4 /* ~¡No puedes esconderte de mi!~ */)
                ForceSpell(Myself,WIZARD_TRUE_SIGHT)
                Wait(2)
		ForceSpell(Player5,WIZARD_DIMENSION_DOOR)
                IncrementGlobal("Localizar","LOCALS",-1)
                SetGlobal("DispaDoor","LOCALS",1)

	RESPONSE #100
                DisplayString(Myself,@4 /* ~¡No puedes esconderte de mi!~ */)
                ForceSpell(Myself,WIZARD_TRUE_SIGHT)
                Wait(2)
		ForceSpell(Player6,WIZARD_DIMENSION_DOOR)
                IncrementGlobal("Localizar","LOCALS",-1)
		SetGlobal("DispaDoor","LOCALS",1)
END


IF
     See(NearestEnemyOfType([PC]))
     Global("DispaDoor","LOCALS",1)
THEN

    RESPONSE #100
        	DisplayString(Myself,26243) // Disparador de conjuros
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
        	ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
                ReallyForceSpell(Myself,WIZARD_SHADOW_DOOR)
                SetGlobal("DispaDoor","LOCALS",0)
                SetGlobalTimer("Cast","LOCALS",6)

    RESPONSE #100
        	DisplayString(Myself,26243) // Disparador de conjuros
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
                ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAOS)
                ReallyForceSpell(Myself,WIZARD_SHADOW_DOOR)
                SetGlobalTimer("Cast","LOCALS",6)
                SetGlobal("DispaDoor","LOCALS",0)
    RESPONSE #100
        	DisplayString(Myself,26243) // Disparador de conjuros
        	ReallyForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
        	ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
                ReallyForceSpell(Myself,WIZARD_SHADOW_DOOR)
                SetGlobalTimer("Cast","LOCALS",6)
                SetGlobal("DispaDoor","LOCALS",0)
    RESPONSE #100
        	DisplayString(Myself,26243) // Disparador de conjuros
        	ReallyForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
                ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAOS)
                ReallyForceSpell(Myself,WIZARD_SHADOW_DOOR)
                SetGlobalTimer("Cast","LOCALS",6)
                SetGlobal("DispaDoor","LOCALS",0)
    RESPONSE #100
        	DisplayString(Myself,26243) // Disparador de conjuros
        	ReallyForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
                ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAOS)
                ReallyForceSpell(Myself,WIZARD_SHADOW_DOOR)
                SetGlobalTimer("Cast","LOCALS",6)
                SetGlobal("DispaDoor","LOCALS",0)
    RESPONSE #100
        	DisplayString(Myself,26243) // Disparador de conjuros
        	ReallyForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
        	ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
                ReallyForceSpell(Myself,WIZARD_SHADOW_DOOR)
                SetGlobalTimer("Cast","LOCALS",6)
                SetGlobal("DispaDoor","LOCALS",0)
END


//****************************************************************************


/////////////////////////////////////////////////////////////////////
// Spell Turning
/////////////////////////////////////////////////////////////////////
IF
    Allegiance(Myself,ENEMY)
    !HasBounceEffects(Myself)
    !HasImmunityEffects(Myself)
    GlobalGT("Turn","LOCALS",0)
THEN
    RESPONSE #100
        IncrementGlobal("Turn","LOCALS",-1)
        SetGlobalTimer("Cast","LOCALS",6)
        SpellNoDec(Myself,WIZARD_SPELL_TURNING)
END


/////////////////////////////////////////////////////////////////////
// Spell: Stoneskin
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Stoneskin","LOCALS",0)
	CheckStatLT(Myself,1,STONESKINS)
THEN
	RESPONSE #100
	  IncrementGlobal("Stoneskin","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
       	  SpellNoDec(Myself,WIZARD_STONE_SKIN)
END

/////////////////////////////////////////////////////////////////////
// Spell: Pro Mag Weapon (if nearby enemy or attacked)
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("RecastProMagWeap","LOCALS")
	GlobalGT("ProMagWeap","LOCALS",0)
	Exists(NearestEnemyOf(Myself))
        HPPercentLT(Myself,100)
	OR(2)
	  See(LastAttackerOf(Myself))
	  Range(NearestEnemyOf(Myself),2)
THEN
	RESPONSE #100
	  IncrementGlobal("ProMagWeap","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
          SetGlobalTimer("RecastProMagWeap","LOCALS",24)
	  SpellNoDec(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
END

/////////////////////////////////////////////////////////////////////
// Spell: Time Stop
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("TimeStop","LOCALS")
	GlobalGT("Time","LOCALS",0)
	See(NearestEnemyOfType([PC]))
	
THEN
	RESPONSE #100
	  IncrementGlobal("Time","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	  SpellNoDec(Myself,WIZARD_TIME_STOP)
          GiveItemCreate("potn55",Myself,1,0,0)   // Curación Superior
          UseItem("potn55",Myself)
	  SetGlobalTimer("TimeStop","LOCALS",18)
END


/////////////////////////////////////////////////////////////////////
// Spell: Cometa
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Cometa","LOCALS",0)
	See(NearestEnemyOfType([PC]))
THEN
	RESPONSE #100
	  IncrementGlobal("Cometa","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	  ReallyForceSpell(LastSeenBy(Myself),WIZARD_COMET)
END

/////////////////////////////////////////////////////////////////////
// Spell: Death Spell
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("DeathSp","LOCALS",0)
	See([GOODCUTOFF.0.0.0.0.SUMMONED])
THEN
	RESPONSE #100
	  IncrementGlobal("DeathSp","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	  SpellNoDec(LastSeenBy(Myself),WIZARD_DEATH_SPELL)
END


/////////////////////////////////////////////////////////////////////
// Spell: Palabras de Poder
/////////////////////////////////////////////////////////////////////


IF
	Allegiance(Myself,ENEMY)
	See(NearestEnemyOf(Myself))
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("PowerWord","LOCALS",0)
	HPLT(LastSeenBy(Myself),60)

THEN
	RESPONSE #100
		SpellNoDec(LastSeenBy(Myself),WIZARD_POWER_WORD_KILL)
                SetGlobalTimer("Cast","LOCALS",6)
	        IncrementGlobal("PowerWord","LOCALS",-1)
	RESPONSE #100
		SpellNoDec(LastSeenBy(Myself),WIZARD_POWER_WORD_STUN)
                SetGlobalTimer("Cast","LOCALS",6)
	        IncrementGlobal("PowerWord","LOCALS",-1)
	RESPONSE #100
		SpellNoDec(ThirdNearestEnemyOf(Myself),WIZARD_POWER_WORD_BLIND)
	        SetGlobalTimer("Cast","LOCALS",6)
                IncrementGlobal("PowerWord","LOCALS",-1)
END

/////////////////////////////////////////////////////////////////////
// Spell: Remove Magic
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Remove","LOCALS",0)
	See(NearestEnemyOfType([PC]))
THEN
	RESPONSE #100
	  IncrementGlobal("Remove","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(NearestEnemyOfType([PC]),2302) // REMOVE_MAGIC
END

/////////////////////////////////////////////////////////////////////
// Spell: Greater Malison
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Malison","LOCALS",0)
	See(NearestEnemyOfType([PC]))
THEN
	RESPONSE #100
	  IncrementGlobal("Malison","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(NearestEnemyOfType([PC]),WIZARD_GREATER_MALISON)
END

/////////////////////////////////////////////////////////////////////
// Spell: Wilting
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Wilt","LOCALS",0)
	See(NearestEnemyOfType([PC]))
THEN
	RESPONSE #100
	  IncrementGlobal("Wilt","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(NearestEnemyOfType([PC]),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

/////////////////////////////////////////////////////////////////////
// Spell: Chaos
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Chaos","LOCALS",0)
	See(NearestEnemyOfType([PC]))
THEN
	RESPONSE #100
	  IncrementGlobal("Chaos","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	  SpellNoDec(NearestEnemyOfType([PC]),WIZARD_CHAOS)
END

/////////////////////////////////////////////////////////////////////
// Spell: Mordy
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Mordy","LOCALS",0)
	OR(2)
	  !CombatCounter(0)
	  See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
	  IncrementGlobal("Mordy","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(Myself,WIZARD_MORDENKAINENS_SWORD)
END

////////////////////////////////////
//      METEORITOS DE MELF
////////////////////////////////////

IF
  Global("MeteMelf","LOCALS",0)
  !HasItem("melfmet", Myself)
THEN 
   RESPONSE #100
   SpellNoDec(Myself,WIZARD_MELF_METEOR)
   SetGlobal("MeteMelf","LOCALS",1)
   IncrementGlobal("MeteMelf","LOCALS",-1)
END
  
IF
  HasItem("melfmet", Myself)
  !HasItemEquiped("CLCK26",NearestEnemyOf(Myself)) // Capa Espejo
  !HasBounceEffects(NearestEnemyOf(Myself))
  ActionListEmpty()
  !StateCheck(Myself,STATE_INVISIBLE)
THEN
  RESPONSE #100
    EquipRanged()
    AttackReevaluate(NearestEnemyOf(Myself),10)
END

/////////////////////////////////////////////////////////////////////
// Spell: Slow
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Slow","LOCALS",0)
	See(NearestEnemyOfType([PC]))
THEN
	RESPONSE #100
	  IncrementGlobal("Slow","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(NearestEnemyOfType([PC]),WIZARD_SLOW)
END

/////////////////////////////////////////////////////////////////////
// Spell: Melf Acid
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("MelfAcid","LOCALS",0)
	See(NearestEnemyOfType([PC]))
THEN
	RESPONSE #100
	  IncrementGlobal("MelfAcid","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(NearestEnemyOfType([PC]),WIZARD_MELF_ACID_ARROW)
END

/////////////////////////////////////////////////////////////////////
// Spell: Rayo de Debilitamiento
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("RayoDebil","LOCALS",0)
	See(NearestEnemyOfType([PC]))
THEN
	RESPONSE #100
	 IncrementGlobal("RayoDebil","LOCALS",-1)
         SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(NearestEnemyOfType([PC]),WIZARD_RAY_OF_ENFEEBLEMENT)
END

/////////////////////////////////////////////////////////////////////
// Spell: Magic Missile
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalGT("Missile","LOCALS",0)
	See(NearestEnemyOfType([PC]))
THEN
	RESPONSE #100
	  IncrementGlobal("Missile","LOCALS",-1)
          SetGlobalTimer("Cast","LOCALS",6)
	 SpellNoDec(NearestEnemyOfType([PC]),WIZARD_MAGIC_MISSILE)
END

/////////////////////////////////////////////////////////////////////
// A bastonazo limpio
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	GlobalTimerExpired("Cast","LOCALS")
        Global("MeteMelf","LOCALS",0)
	Global("Missile","LOCALS",0)
	Global("Time","LOCALS",0)
        Global("Mordy","LOCALS",0)
        Global("MelfAcid","LOCALS",0)
	See(NearestEnemyOfType([PC]))
THEN
	RESPONSE #100
	  EquipMostDamagingMelee()
	  AttackReevaluate(NearestEnemyOf(Myself),15)
END



/////////////////////////////////////////////////////////////////////
//     ¡¡ZAKI AL RESCATE!!
/////////////////////////////////////////////////////////////////////

IF
        
        !See([GOODCUTOFF])
THEN
	RESPONSE #100
           MoveToObject(Player1)
END

/////////////////////////////////////////////////////////////////////
// Wait for death. :-) Don't let some other BG script take over. 
/////////////////////////////////////////////////////////////////////
IF
	True()
THEN
	RESPONSE #100
	  NoAction()
END

//******************************************************************************
//******************************************************************************



////////////////////////////////////////////////
//            PIDIENDO AYUDA
/////////////////////////////////////////////////

IF
	See(NearestEnemyOf(Myself))
	InMyGroup(NearestEnemyOf(Myself))
	HPPercentLT(Myself,30)
	!Range(NearestEnemyOf(Myself),8)
	!GlobalTimerNotExpired("Whined","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("Whined","LOCALS",6)
		DisplayStringHead(Myself,46151) // ¡ayuda!
		Shout(ASSIST)
	RESPONSE #100
		SetGlobalTimer("Whined","LOCALS",6)
		DisplayStringHead(Myself,49763) // Necesito ayuda
		Shout(ASSIST)
	RESPONSE #100
		SetGlobalTimer("Whined","LOCALS",6)
		DisplayStringHead(Myself,49764) // ¡Defendedme!
		Shout(ASSIST)
	RESPONSE #100
		SetGlobalTimer("Whined","LOCALS",6)
		DisplayStringHead(Myself,49765) // ¡Matad a mi atacante!
		Shout(ASSIST)
END

/////////////////////////////////////////
//           HUIDA
/////////////////////////////////////////

IF
	BreakingPoint()
THEN
	RESPONSE #100
		DisplayStringHead(Myself,49766) // No puedo aceptarlo
		Shout(ASSIST)
	RESPONSE #100
		DisplayStringHead(Myself,49767) // Ya basta
		Shout(ASSIST)
	RESPONSE #100
		DisplayStringHead(Myself,49768) // ¡Piedad!
		Shout(ASSIST)
END

/////////////////////////////////////////////
//           POCIONES
/////////////////////////////////////////////

IF
	HPPercentLT(Myself,50)
	!GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
	HasItem("POTN55",Myself) // Poción de curación superior
THEN
	RESPONSE #100
		SetGlobalTimer("DrankPotionTimer","LOCALS",6)
		DisplayStringHead(Myself,46150) // se bebe una poción
		UseItem("POTN55",Myself) // Poción de curación superior
END

IF
	HPPercentLT(Myself,50)
	!GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
	HasItem("POTN52",Myself) // Poción de curación extra
THEN
	RESPONSE #100
		SetGlobalTimer("DrankPotionTimer","LOCALS",6)
		DisplayStringHead(Myself,46150) // se bebe una poción
		UseItem("POTN52",Myself) // Poción de curación extra
END


//////////////////////////////////////////
//   PIDIENDO CURACIÓN
//////////////////////////////////////////


IF
	See(NearestEnemyOf(Myself))
	OR(2)
		Range(NearestMyGroupOfType,20)
		See(NearestMyGroupOfType([0.0.0.CLERIC_ALL]))
	HPPercentLT(Myself,35)
	!GlobalTimerNotExpired("CalledForHealing","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("CalledForHealing","LOCALS",6)
		DisplayStringHead(Myself,72808) // ¡Necesito curación!
		Shout(HEALME)
		Continue()
END


IF
	Heard(NearestMyGroupOfType,HEALME)
	!StateCheck(LastHeardBy(Myself),STATE_REALLY_DEAD)
	HPPercentGT(Myself,75)
	See(LastHeardBy(Myself))
	Range(LastHeardBy(Myself),10)
	!Range(NearestMyGroupOfType([0.0.0.CLERIC_ALL]),20) // No hay clerigos
	!NumItemsGT("POTN55",LastHeardBy(Myself),0) // Poción de curación superior
	NumItemsGT("POTN55",Myself,1) 
	!NumItemsGT("POTN52",LastHeardBy(Myself),0) // Poción de curación extra
	NumItemsGT("POTN52",Myself,1)
	!GlobalTimerNotExpired("GaveAwayPotion","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("GaveAwayPotion","LOCALS",12)
		DisplayStringHead(Myself,72809) // ¡Aquí tienes! ¡Cógelo!
		GiveItem("POTN55",LastHeardBy(Myself)) // Poción de curación superior
                GiveItem("POTN52",LastHeardBy(Myself)) // Poción de curación extra
		SmallWait(2)
		ActionOverride(LastHeardBy(Myself),DisplayStringHead(Myself,73896)) // ¡Lo tengo!
		Continue()
END



//***********************************************************************
//***********************************************************************